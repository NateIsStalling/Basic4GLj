plugins {
    id 'java'
}

group 'com.basic4gl.library'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

configurations {
    release.extendsFrom configurations.default
}

dependencies {
    implementation fileTree(dir: "libs", include: "*.jar")

    implementation project(":runtime")
    implementation project(":compiler")

    implementation project(":debugProtocol")

    // for debugger
    implementation ('com.google.code.gson:gson:2.10')
    implementation ('javax.websocket:javax.websocket-api:1.0')
    implementation ('org.eclipse.jetty.websocket:javax-websocket-client-impl:9.4.49.v20220914')


    // for PCX image support
    implementation("org.apache.commons:commons-imaging:1.0-alpha3")

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

task copyToLib( type: Copy ) {
    into "$buildDir/libs/lib"
    from configurations.release
}

jar {
    dependsOn copyToLib

    def classPath = configurations.release.files.collect { it.name }.join(' ')

    from {
        (configurations.release).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }

    manifest {
        attributes (
            'Main-Class': 'com.basic4gl.library.desktopgl.GLTextGridWindow',
            'Class-Path': classPath
        )
    }

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    exclude 'META-INF/.RSA', 'META-INF/.SF', 'META-INF/*.DSA'

    rootProject.ext.set("libraryJarPath", jar.archiveFile.get())

    doLast {
        new File(projectDir, "class-path.txt").text = classPath
    }
}

test {
    useJUnitPlatform()
}